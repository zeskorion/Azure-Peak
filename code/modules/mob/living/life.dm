/mob/living/proc/Life(seconds, times_fired)
	set waitfor = FALSE
	set invisibility = 0

	// Sleep gate: skip Life() for AI-off NPCs to save cycles, but only if fully conscious.
	// If not conscious, we must keep running Life() so wounds bleed, blood drops, and update_stat() can transition us.
	if(!client && stat == CONSCIOUS && ai_controller && ai_controller.ai_status == AI_STATUS_OFF)
		return

	SEND_SIGNAL(src, COMSIG_LIVING_LIFE, seconds, times_fired)

	if((movement_type & FLYING) && !(movement_type & FLOATING))	//TODO: Better floating
		float(on = TRUE)

	if(client)
		var/turf/T = get_turf(src)
		if(!T)
			var/msg = "[ADMIN_LOOKUPFLW(src)] was found to have no .loc with an attached client, if the cause is unknown it would be wise to ask how this was accomplished."
			message_admins(msg)
			send2irc_adminless_only("Mob", msg, R_ADMIN)
			log_game("[key_name(src)] was found to have no .loc with an attached client.")

		// This is a temporary error tracker to make sure we've caught everything
		else if(registered_z != T.z)
#ifdef TESTING
			message_admins("[ADMIN_LOOKUPFLW(src)] has somehow ended up in Z-level [T.z] despite being registered in Z-level [registered_z]. If you could ask them how that happened and notify coderbus, it would be appreciated.")
#endif
			log_game("Z-TRACKING: [src] has somehow ended up in Z-level [T.z] despite being registered in Z-level [registered_z].")
			update_z(T.z)
	else if(registered_z)
		log_game("Z-TRACKING: [src] of type [src.type] has a Z-registration despite not having a client.")
		update_z(null)

	if(notransform)
		return
	if(!loc)
		return

	handle_breathing()

	// SIMPLE WOUNDS
	if(HAS_TRAIT(src, TRAIT_SIMPLE_WOUNDS))
		handle_wounds()
		handle_embedded_objects()
		handle_blood()
		//passively heal even wounds with no passive healing
		heal_wounds(1)

	// REGEN RESTRICTIONS -- Starving, or being on fire/silverfired.
	var/noregen = nutrition < NUTRITION_LEVEL_STARVING - 75 || has_status_effect(/datum/status_effect/fire_handler/fire_stacks/sunder) || has_status_effect(/datum/status_effect/fire_handler/fire_stacks/sunder/blessed)

	// BLACKBLOOD REGEN -- Every tick from this will cost hunger across three different instances, so the more hurt, the more hungry you'll become. If you're not hurt, then this basically is skipped.
	if(stat != DEAD && HAS_TRAIT(src, TRAIT_BLACKBLOOD) && !HAS_TRAIT(src, TRAIT_PARALYSIS) && !noregen)
		handle_wounds()
		var/list/wounds = get_wounds() // literally was calling get_wounds() 3x so just stuffing this into a list and being done with it
		var/has_brute = getBruteLoss() > 0 // teehee D:
		var/has_healable_wound = FALSE
		var/has_bleeding_wound = FALSE
		for(var/datum/wound/wound as anything in wounds)
			if(!istype(wound, /datum/wound/slash/incision) && wound?.severity <= WOUND_SEVERITY_SEVERE)
				has_healable_wound = TRUE
			if(wound?.bleed_rate > 0)
				has_bleeding_wound = TRUE
		if(has_brute || has_healable_wound || has_bleeding_wound)
			var/mob/living/carbon/human/H = src
			var/healing_multiplier = max(0.5 ** (
				(in_combat_until > world.time) + (H.highest_ac_worn() > ARMOR_CLASS_LIGHT) + has_stress_event(/datum/stressevent/sun_sensitivity) + has_stress_event(/datum/stressevent/thirst) + has_stress_event(/datum/stressevent/inq_trauma)), 0.15)
			if(HAS_TRAIT(src, TRAIT_NOHUNGER))
				healing_multiplier = 0.15
			// Wound healing.
			if(has_healable_wound)
				for(var/datum/wound/wound as anything in wounds)
					if(!istype(wound, /datum/wound/slash/incision) && wound?.severity <= WOUND_SEVERITY_SEVERE)
						wound.heal_wound(healing_multiplier)
			// Brute healing.
			if(has_brute)
				var/healing_cost = NUTRITION_LEVEL_FULL * 0.00125 * healing_multiplier
				heal_overall_damage(3 * healing_multiplier, 0, 0)
				nutrition = max(0, nutrition - healing_cost)
			// Bleeding/sealing.
			if(has_bleeding_wound)
				var/sealing_cost = NUTRITION_LEVEL_FULL * 0.00125 * healing_multiplier
				for(var/datum/wound/wound as anything in wounds)
					if(wound.bleed_rate > 0)
						var/bleed_heal = max(wound.bleed_rate * 0.2, 0.1) * healing_multiplier
						wound.set_bleed_rate(max(wound.bleed_rate - bleed_heal, 0.025))
						if(wound.bleed_rate <= 0 && wound.sew_threshold)
							wound.sew_progress = wound.sew_threshold
							wound.sew_wound()
							to_chat(src, span_artery("<i>The [wound] stitched itself...</i>"))
				nutrition = max(0, nutrition - sealing_cost)

	// LYCAN RESILIENCE -- This had a nasty return which was causing an awful glitch. Don't use return on Life(), pls.
	if(!stat && HAS_TRAIT(src, TRAIT_LYCANRESILENCE) && !HAS_TRAIT(src, TRAIT_PARALYSIS) && !noregen)
		handle_wounds()
		if(blood_volume > BLOOD_VOLUME_SURVIVE)
			for(var/datum/wound/wound as anything in get_wounds())
				if(!istype(wound, /datum/wound/slash/incision))
					wound.heal_wound(3)

	// DEADITE REGEN -- Same as above, but a victim of copypasta. Don't use return on Life(), pls.
	if(!stat && HAS_TRAIT(src, TRAIT_DEADITE))
		var/deadite_on_fire = has_status_effect(/datum/status_effect/fire_handler/fire_stacks/sunder) || has_status_effect(/datum/status_effect/fire_handler/fire_stacks) || has_status_effect(/datum/status_effect/fire_handler/fire_stacks/sunder/blessed)
		if(!deadite_on_fire)
			handle_wounds()
			heal_overall_damage(3, 2)
			for(var/datum/wound/wound as anything in get_wounds())
				wound.heal_wound(0.5)

	if(blood_volume <= BLOOD_VOLUME_SURVIVE && stat)
		handle_passive_blood()

	if(QDELETED(src)) // diseases can qdel the mob via transformations
		return

	handle_environment()

	//Random events (vomiting etc)
	handle_random_events()

	handle_traits() // eye, ear, brain damages
	handle_status_effects() //all special effects, stun, knockdown, jitteryness, hallucination, sleeping, etc

	update_sneak_invis()

	if(machine)
		machine.check_eye(src)

	check_drowning()

	if(stat != DEAD)
		return 1

/mob/living/proc/handle_passive_blood()
	#define MAX_PASSIVE_BLOOD_HEAL	10
	#define MIN_PASSIVE_BLOOD_HEAL	0

	var/passive_regen_rate = MIN_PASSIVE_BLOOD_HEAL
	if(nutrition <= NUTRITION_LEVEL_HUNGRY)
		passive_regen_rate -= 5
	else
		passive_regen_rate += 5

	if(hydration <= HYDRATION_LEVEL_THIRSTY)
		passive_regen_rate -= 5
	else
		passive_regen_rate += 5

	passive_regen_rate = CLAMP(passive_regen_rate, MIN_PASSIVE_BLOOD_HEAL, MAX_PASSIVE_BLOOD_HEAL)

	blood_volume += passive_regen_rate

	#undef MAX_PASSIVE_BLOOD_HEAL
	#undef MIN_PASSIVE_BLOOD_HEAL

/mob/living/proc/check_drowning()
	if(istype(loc, /turf/open/water))
		handle_inwater(loc)

/mob/living/carbon/human/check_drowning()
	if(isdullahan(src))
		var/mob/living/carbon/human = src
		var/datum/species/dullahan/dullahan = human.dna.species
		if(dullahan.headless)
			var/obj/item/bodypart/head/dullahan/drownrelay = dullahan.my_head
			if(!drownrelay)
				return
			if(istype(drownrelay.loc, /turf/open/water))
				handle_inwater(drownrelay.loc, extinguish = FALSE, force_drown = TRUE)
			if(istype(loc, /turf/open/water)) // Extinguish ourselves if our body is in water.
				extinguish_mob()
			return
	. =..()

/mob/living/proc/handle_breathing()
	return TRUE

/mob/living/carbon/handle_breathing()
	if(HAS_TRAIT(src, TRAIT_NOBREATH))
		return TRUE
	var/obj/item/organ/lungs/lung = getorganslot(ORGAN_SLOT_LUNGS)
	if(!lung || (lung.organ_flags & ORGAN_FAILING) || ((lung.organ_flags & ORGAN_LUX) && check_lux_organ_cap()))
		adjustOxyLoss(5)
		emote("choke")
		return FALSE
	return TRUE

/mob/living/proc/DeadLife()
	set invisibility = 0
	if (notransform)
		return
	if(!loc)
		return
	if(HAS_TRAIT(src, TRAIT_SIMPLE_WOUNDS))
		handle_wounds()
		handle_embedded_objects()
		handle_blood()
	update_sneak_invis()
	if(istype(loc, /turf/open/water))
		handle_inwater(loc)

/mob/living/proc/handle_random_events()
	return

/mob/living/proc/handle_environment()
	return

/mob/living/proc/handle_wounds()
	for(var/datum/wound/wound as anything in get_wounds())
		if(!wound)
			continue

		if(stat != DEAD)
			wound.on_life()
		else
			wound.on_death()

/obj/item/proc/on_embed_life(mob/living/user, obj/item/bodypart/bodypart)
	return

/mob/living/proc/handle_embedded_objects()
	for(var/obj/item/embedded as anything in simple_embedded_objects)
		if(embedded.on_embed_life(src))
			continue

		if(prob(embedded.embedding.embedded_pain_chance))
			if((embedded.is_silver || (embedded.is_even_lesser_silver && is_npc(src))) && HAS_TRAIT(src, TRAIT_SILVER_WEAK) && !has_status_effect(STATUS_EFFECT_ANTIMAGIC))
				var/datum/component/silverbless/psyblessed = embedded.GetComponent(/datum/component/silverbless)
				adjust_fire_stacks(1, psyblessed?.is_blessed ? /datum/status_effect/fire_handler/fire_stacks/sunder/blessed : /datum/status_effect/fire_handler/fire_stacks/sunder)
			to_chat(src, span_danger("[embedded] in me hurts!"))

		if(prob(embedded.embedding.embedded_fall_chance))
			simple_remove_embedded_object(embedded)
			to_chat(src,span_danger("[embedded] falls out of me!"))

//this updates all special effects: knockdown, druggy, stuttering, etc..
/mob/living/proc/handle_status_effects()
	if(confused)
		confused = max(confused - 1, 0)
	if(slowdown)
		slowdown = max(slowdown - 1, 0)
	if(slowdown <= 0)
		remove_movespeed_modifier(MOVESPEED_ID_LIVING_SLOWDOWN_STATUS)

/mob/living/proc/handle_traits()
	//Eyes
	if(eye_blind)	//blindness, heals slowly over time
		if(HAS_TRAIT_FROM(src, TRAIT_BLIND, EYES_COVERED)) //covering your eyes heals blurry eyes faster
			adjust_blindness(-3)
		else if(!stat && !(HAS_TRAIT(src, TRAIT_BLIND)))
			adjust_blindness(-1)
	else if(eye_blurry)			//blurry eyes heal slowly
		adjust_blurriness(-1)

/mob/living/proc/update_damage_hud()
	return
